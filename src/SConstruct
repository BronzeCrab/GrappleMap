import os

env = Environment(ENV=os.environ, CCFLAGS='-Wall -Wextra -pedantic -std=c++1y -DNDEBUG -O3 -DUSE_FTGL')
# env = Environment(CCFLAGS='-Wall -Wextra -pedantic -std=c++1y -g')

common = env.Object(['graph.cpp', 'graph_util.cpp', 'positions.cpp', 'viables.cpp', 'persistence.cpp', 'paths.cpp'])
rendering = env.Object('rendering.cpp')
images = env.Object('images.cpp')
cmdlibs = ['boost_program_options']
guilibs = ['GL', 'GLU', 'glfw', 'ftgl'] + cmdlibs

editor     = env.Program('grapplemap-editor', ['editor.cpp', rendering, common], LIBS=guilibs)
playback   = env.Program('grapplemap-playback', ['playback.cpp', rendering, common], LIBS=guilibs)
todot      = env.Program('grapplemap-todot', ['todot.cpp', common], LIBS=cmdlibs)
dbtojs     = env.Program('grapplemap-dbtojs', ['dbtojs.cpp', common], LIBS=cmdlibs)
mkpospages = env.Program('grapplemap-mkpospages', ['mkpospages.cpp', images, rendering, common],
				LIBS = ['OSMesa', 'GLU', 'ftgl', 'boost_program_options', 'png', 'boost_filesystem', 'boost_system'])
mkvid      =env.Program('grapplemap-mkvid', ['makevideo.cpp', images, rendering, common],
				LIBS = ['OSMesa', 'GLU', 'boost_program_options', 'png', 'boost_filesystem', 'boost_system', 'ftgl'])

env.Alias('noX', [dbtojs, mkpospages, mkvid]);
